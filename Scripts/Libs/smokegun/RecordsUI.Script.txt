#Include "MathLib" as ML
#Include "Libs/Nadeo/Log.Script.txt" as Log
#Include "Libs/Nadeo/Layers2.Script.txt" as Layers
#Include "Libs/Nadeo/CustomUI.Script.txt" as CustomUI

/*********************************************
	CONSTANTS
*********************************************/
#Const	Version		"2017-07-28"
#Const	ScriptName	"RecordsUI.Script.txt"

#Const	RecordsPerPage	32

declare Text[][] G_Records;
declare Integer G_PageNb;

Text Private_GetRecordsList() {
	return """
<frame id="Frame_Records_Panel" hidden="1">
	<frame id="Frame_Heading">
		<label pos="0 39" size="50 10" text="Records" textsize="9" textcolor="FFFFFFFF" maxline="1" halign="center" valign="bottom" style="TextTitle3" textfont="Oswald" textemboss="1" translate="0"/>
		<quad pos="0 40" z-index="-1" size="220 0.5" bgcolor="000000AA" halign="center"/>
		<frameinstance modelid="records_heading" pos="5 37"/>
		<frameinstance modelid="records_heading" pos="-105 37"/>
	</frame>
	<frame pos="0 37" id="Frame_Records">
		<frameinstance modelid="record" pos="-105 -5"   id="record0"/>
		<frameinstance modelid="record" pos="-105 -10" id="record1"/>
		<frameinstance modelid="record" pos="-105 -15" id="record2"/>
		<frameinstance modelid="record" pos="-105 -20" id="record3"/>
		<frameinstance modelid="record" pos="-105 -25" id="record4"/>
		<frameinstance modelid="record" pos="-105 -30" id="record5"/>
		<frameinstance modelid="record" pos="-105 -35" id="record6"/>
		<frameinstance modelid="record" pos="-105 -40" id="record7"/>
		<frameinstance modelid="record" pos="-105 -45" id="record8"/>
		<frameinstance modelid="record" pos="-105 -50" id="record9"/>
		<frameinstance modelid="record" pos="-105 -55" id="record10"/>
		<frameinstance modelid="record" pos="-105 -60" id="record11"/>
		<frameinstance modelid="record" pos="-105 -65" id="record12"/>
		<frameinstance modelid="record" pos="-105 -70" id="record13"/>
		<frameinstance modelid="record" pos="-105 -75" id="record14"/>
		<frameinstance modelid="record" pos="-105 -80" id="record15"/>
		<frameinstance modelid="record" pos="5 -5"   id="record16"/>
		<frameinstance modelid="record" pos="5 -10" id="record17"/>
		<frameinstance modelid="record" pos="5 -15" id="record18"/>
		<frameinstance modelid="record" pos="5 -20" id="record19"/>
		<frameinstance modelid="record" pos="5 -25" id="record20"/>
		<frameinstance modelid="record" pos="5 -30" id="record21"/>
		<frameinstance modelid="record" pos="5 -35" id="record22"/>
		<frameinstance modelid="record" pos="5 -40" id="record23"/>
		<frameinstance modelid="record" pos="5 -45" id="record24"/>
		<frameinstance modelid="record" pos="5 -50" id="record25"/>
		<frameinstance modelid="record" pos="5 -55" id="record26"/>
		<frameinstance modelid="record" pos="5 -60" id="record27"/>
		<frameinstance modelid="record" pos="5 -65" id="record28"/>
		<frameinstance modelid="record" pos="5 -70" id="record29"/>
		<frameinstance modelid="record" pos="5 -75" id="record30"/>
		<frameinstance modelid="record" pos="5 -80" id="record31"/>
	</frame>
	<frame id="Frame_Footer">
		<quad pos="0 -50" size="5 5" style="Icons64x64_1" substyle="ArrowNext" valign="bottom" scriptevents="1" halign="left" id="records_next_button"/>
		<quad pos="0 -50" size="5 5" style="Icons64x64_1" substyle="ArrowPrev" valign="bottom" scriptevents="1" halign="right" id="records_prev_button"/>
	</frame>
	<frame id="Frame_Background">
		<quad z-index="-1" size="220 100" bgcolor="000000FF" halign="center" valign="center"  opacity="0.5"/>
		<quad pos="0 35" z-index="0" size="0.5 80" bgcolor="FFFF" halign="center" valign="top" opacity="0.1"/>
		<quad pos="0 35" z-index="-1" size="220 80" bgcolor="000000AA" halign="center" valign="top"/>
	</frame>
</frame>
<framemodel id="records_heading">
	<label pos="5 0" size="10 5" text="Rank" valign="center2" textsize="1" maxline="1" halign="center" textfont="Oswald"/>
	<label pos="35 0" size="50 5" text="Name" valign="center2" halign="center" textsize="1" textfont="Oswald"/>
	<label pos="65 0" size="10 5" text="Try" valign="center2" halign="center" textsize="1" textfont="Oswald"/>
	<label pos="75 0" size="10 5" text="RS" valign="center2" halign="center" textsize="1" textfont="Oswald"/>
	<label pos="90 0"  size="20 5" text="Time" valign="center2" halign="center" textsize="1" textfont="OswaldMono"/>
</framemodel>
<framemodel id="record">
	<label pos="5 0" size="10 5" text="123456." valign="center2" halign="center" maxline="1" textsize="1" id="rank" textfont="OswaldMono"/>
	<label pos="35 0" size="50 5" text="ObsPresidente" valign="center2" halign="center" textsize="1" id="name" textfont="Oswald"/>
	<label pos="65 0" size="10 5" text="1" valign="center2" halign="center" textsize="1" id="try" textfont="OswaldMono"/>
	<label pos="75 0" size="10 5" text="0" valign="center2" halign="center" textsize="1" id="rs" textfont="OswaldMono"/>
	<label pos="90 0" size="20 5" text="00:00:01" valign="center2" halign="center" textsize="1" id="time" textfont="OswaldMono"/>
	<label pos="50 0" size="110 5" valign="center2" halign="center" text="" action="highlight" focusareacolor1="FFFFFF00" focusareacolor2="FFFFFFFF" textfont="OswaldMono" opacity="0.1"/>
</framemodel>
	""";
}

Text Private_GetSmallRecords() {
	return """
<frame id="Frame_Small_Records" class="LibCustomUI_Module">
	<frame id="Frame_Background"halign="center" valign="center2" pos="139 75">
		<quad z-index="-1" size="44 67" style="BgRaceScore2" substyle="BgCardPlayer" halign="center" valign="top"/>
		<label pos="0 -1" size="40 5" text="Records" style="TextTitle3" textsize="4" halign="center" textfont="OswaldMono" maxline="1" valign="top" textemboss="1"/>
	</frame>
	<frame id="Frame_Records" pos="139 67" z-index="1">
		<frameinstance modelid="small_record" pos="0 0" id="record0" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -4" id="record1" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -8" id="record2" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -12" id="record3" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -16" id="record4" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -20" id="record5" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -24" id="record6" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -28" id="record7" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -32" id="record8" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -36" id="record9" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -40" id="record10" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -44" id="record11" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -48" id="record12" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -52" id="record13" hidden="1"/>
		<frameinstance modelid="small_record" pos="0 -56" id="record14" hidden="1"/>
	</frame>
	<framemodel id="small_record">
		<label id="rank" pos="-20 0" size="6 4" halign="left" valign="center2" style="TextCardSmallScores2" textsize="1" text="1." textfont="OswaldMono"/>
		<label id="name" pos="-14 0" size="20 4" halign="left" valign="center2" textsize="1" text="mon super pseudo" textfont="Oswald"/>
		<label id="time" pos="20 0" size="14 4" halign="right" valign="center2" style="TextTitle2" textsize="1" text="00:00:01" textfont="OswaldMono"/>
	</framemodel>
</frame>
	""";
}

Text Private_GetPlayerLogin(Integer _Index) {
	if(_Index < 0 || _Index >= G_Records.count) return "Error wrong idx";
	return G_Records[_Index][0];
}

Text Private_GetPlayerNickname(Integer _Index) {
	if(_Index < 0 || _Index >= G_Records.count) return "Error wrong idx";
	return G_Records[_Index][1];
}

Text Private_GetTime(Integer _Index) {
	if(_Index < 0 || _Index >= G_Records.count) return "Error wrong idx";
	return G_Records[_Index][2];
}

Text Private_GetRs(Integer _Index) {
	if(_Index < 0 || _Index >= G_Records.count) return "Error wrong idx";
	return G_Records[_Index][3];
}

Text Private_GetTry(Integer _Index) {
	if(_Index < 0 || _Index >= G_Records.count) return "Error wrong idx";
	return G_Records[_Index][4];
}

Text Private_GetTimeago(Integer _Index) {
	if(_Index < 0 || _Index >= G_Records.count) return "Error wrong idx";
	return G_Records[_Index][5];
}

Text Private_Vec(Vec2 _Vector) {
	return _Vector.X^" "^_Vector.Y;
}

Void UpdateNetVars(Integer _RecordIdx, CSmPlayer _Player, Boolean _Empty) {
	declare line_color = "$FFF";
	
	if(_RecordIdx == 0) {
		line_color = "$CC0";
	} else if(_RecordIdx == 1) {
		line_color = "$CCC";
	} else if(_RecordIdx == 2) {
		line_color = "$963";
	}
	
	// Rank
	declare rank = "";
	if(!_Empty) {
		rank = line_color^_RecordIdx + 1^".";
	}

	// Name
	declare name = "";
	if(_Empty) {
		name = _Player.User.Name;
	} else {
		name = Private_GetPlayerNickname(_RecordIdx);
	}

	// Time
	declare time = line_color^"--:--:--";
	if(!_Empty) {
		time = line_color^Private_GetTime(_RecordIdx);
	}
	
	declare netwrite SmallRecords for _Player = Text[][];
	SmallRecords.add([rank, name, time]);
}


Void UpdateNetVars(Integer _RecordIdx, CSmPlayer _Player) {
	return UpdateNetVars(_RecordIdx, _Player, False);
}

Void DisplayPlayersFromTo(Integer _Start, Integer _End, CSmPlayer _Player) {
	// Check that both index are valid
	if(_Start >= 0 && _Start < G_Records.count && _End > 0 && _End >= _Start) {

		// Need a substitute variable because params are readonly
		declare End = _End;
		if(End >= G_Records.count) {
			End = G_Records.count - 1;
		}
				
		declare netwrite SmallRecords for _Player = Text[][];
		// Display player records
		for(Index, _Start, End) {
			if(SmallRecords.count >= 15) break;
			UpdateNetVars(Index, _Player);
		}
	} else {
		Log::Log("Error when displaying records. Wrong index, StartIdx "^_Start^"| EndIdx "^_End^"| Records "^G_Records.count);
	}
}

Void ChangePage(CUIConfig _UI, Integer dir) {
	if(dir > 0 && G_Records.count > (G_PageNb+1) * RecordsPerPage) {
		G_PageNb =  G_PageNb + 1;
	} else if (dir < 0 && G_PageNb > 0) {
		G_PageNb = G_PageNb - 1;
	}
	
	declare netwrite RecordsList for _UI = Text[][];
	RecordsList.clear();
	
	for(Idx, G_PageNb * RecordsPerPage, G_PageNb * RecordsPerPage + RecordsPerPage - 1) {
		if(Idx >= G_Records.count) {
			break;
		}
		RecordsList.add(
			[ Idx+1^""
			, Private_GetPlayerNickname(Idx)
			, Private_GetTime(Idx)
			, Private_GetTry(Idx)
			, Private_GetRs(Idx)
			, Private_GetTimeago(Idx)
		  ]
		);
	}
	
	declare netwrite Net_RecordsUpdated for _UI = 0;
	Net_RecordsUpdated = Now;
}

Void UpdateManialink(CSmPlayer _Player) {
	// Find player record
	declare CurrentPlayerRecordId = -1;
	foreach(Idx => Record in G_Records) {
		if(Private_GetPlayerLogin(Idx) == _Player.User.Login) {
			CurrentPlayerRecordId = Idx;
			break;
		}
	}
	
	declare netwrite SmallRecords for _Player = Text[][];
	SmallRecords.clear();

	// Display top X players
	DisplayPlayersFromTo(0, 2, _Player);

	// The player has no record
	if(CurrentPlayerRecordId < 0) {
		// We need to set end to records count - 2 to leave a blank line
		// for the current player
						
		// Display records from the end
		if(G_Records.count >= 15) {
			DisplayPlayersFromTo(G_Records.count - 11, G_Records.count - 1, _Player);
			
		// Display all records
		} else {
			DisplayPlayersFromTo(3, G_Records.count - 1, _Player);
		}
		
		// Add an empty line for the player
		UpdateNetVars(-1, _Player, True);

	// The player has record
	} else {
		if(CurrentPlayerRecordId < 15) {
			// Display all records
			DisplayPlayersFromTo(3, G_Records.count, _Player);
		} else {

			// Display only records around the player
			declare upper_half = 6;
			declare records_below = G_Records.count - CurrentPlayerRecordId - 1;

			// There is a gap between the last record and the widget
			// So display more records to fill the gap
			if(records_below < upper_half) {
				upper_half += upper_half - records_below;
			}

			DisplayPlayersFromTo(CurrentPlayerRecordId + 1 - upper_half, G_Records.count, _Player);
		}
	}
	
		// Update UI
	declare UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;
		
	ChangePage(UI, 0);
	
	declare netwrite Net_RecordsUpdated for UI = 0;
	Net_RecordsUpdated = Now;
}

Text CreateManialink(Text _Title) {
	return """
<manialink version="3">
<frame>
	{{{Private_GetSmallRecords()}}}
</frame>
<frame>
	{{{Private_GetRecordsList()}}}
</frame>
<script><!--
		Void UpdateFrameTab(CUIConfig UI, CMlPage Page, Text TabKey, Text FrameTabId)
		{
			declare netread Boolean _TabsLib_UseTabs for UI;
			if (! _TabsLib_UseTabs) return;
			
			declare Boolean _TabsLib_ScoresLayerIsVisible 	for UI;
			declare Boolean _TabsLib_AltLayerIsVisible 		for UI;
			declare Text 	_TabsLib_CurrentTab 			for UI;
			declare netread Text _TabsLib_ScoresTableTab 	for UI;
			
			declare Boolean ShowCurrentTab = _TabsLib_AltLayerIsVisible && (_TabsLib_CurrentTab == TabKey);
			
			if(TabKey == _TabsLib_ScoresTableTab) 
			{
				// log("_TabsLib_ScoresTableTab: "^_TabsLib_ScoresTableTab);
				ShowCurrentTab = _TabsLib_ScoresLayerIsVisible || 
					(_TabsLib_AltLayerIsVisible && (_TabsLib_CurrentTab == _TabsLib_ScoresTableTab));
			}
		
			declare MainFrame <=> (Page.GetFirstChild(FrameTabId) as CMlFrame);
			if(MainFrame == Null) return;
			
			if(ShowCurrentTab) {
				MainFrame.Show();
			}
			else {
				MainFrame.Hide();
			}
		}

	main() {
		declare LastUpdate = 0;
		
		// Records widget on the right
		declare Frame_Small_Records <=> (Page.GetFirstChild("Frame_Small_Records") as CMlFrame);
		declare Small_Records <=> (Frame_Small_Records.GetFirstChild("Frame_Records") as CMlFrame);
		
		// Records list
		declare Frame_Records_Panel <=> (Page.GetFirstChild("Frame_Records_Panel") as CMlFrame);
		declare Records_Panel <=> (Frame_Records_Panel.GetFirstChild("Frame_Records") as CMlFrame);
		
		{{{CustomUI::InjectMLInit()}}}
		
		while (True) {
			yield;
			
			foreach(Event in PendingEvents) {
				if(Event.Type == CMlScriptEvent::Type::MouseClick) {
					switch(Event.ControlId) {					
						case "records_prev_button": {
							SendCustomEvent("RecordsList_Prev_Page", [""]);
						}						
						case "records_next_button": {
							SendCustomEvent("RecordsList_Next_Page", [""]);
						}						
					}
				}
			}
			
			UpdateFrameTab(UI, Page, "RecordsList", "Frame_Records_Panel");
			
			{{{CustomUI::InjectMLLoop()}}}
			
			declare netread Net_RecordsUpdated for UI = 0;
			if(Net_RecordsUpdated < LastUpdate && GameTime <= LastUpdate + 10000) continue;
			LastUpdate = GameTime;
			
			declare LocalPlayer <=> InputPlayer;
			if (GUIPlayer != Null) LocalPlayer <=> GUIPlayer;			
			if(LocalPlayer == Null) continue;
			
			// UPDATE RECORDS IN EACH WIDGET
			
			declare netread SmallRecords for LocalPlayer = Text[][];
			foreach(Idx => Control in Small_Records.Controls) {
				Control.Visible = Idx < SmallRecords.count;
				if(!Control.Visible) {
					continue;
				}
				
				declare frame <=> (Control as CMlFrame);				
				declare rankLabel <=> (frame.GetFirstChild("rank") as CMlLabel);
				declare nameLabel <=> (frame.GetFirstChild("name") as CMlLabel);
				declare timeLabel <=> (frame.GetFirstChild("time") as CMlLabel);

				rankLabel.SetText(SmallRecords[Idx][0]);
				nameLabel.SetText(SmallRecords[Idx][1]);
				timeLabel.SetText(SmallRecords[Idx][2]);
				
				if(SmallRecords[Idx][1] == LocalPlayer.User.Name) {
				
				} else {
				
				}
			}

			declare netread RecordsList for UI = Text[][];
			for(Idx, 0, {{{RecordsPerPage-1}}}) {
				declare frame = (Records_Panel.GetFirstChild("record"^Idx) as CMlFrame);
				
				if(Idx < RecordsList.count) {
					declare Record = RecordsList[Idx];
					declare rankLabel <=> (frame.GetFirstChild("rank") as CMlLabel);
					declare nameLabel <=> (frame.GetFirstChild("name") as CMlLabel);
					declare timeLabel <=> (frame.GetFirstChild("time") as CMlLabel);
					declare tryLabel <=> (frame.GetFirstChild("try") as CMlLabel);
					declare rsLabel <=> (frame.GetFirstChild("rs") as CMlLabel);
					
					rankLabel.SetText(Record[0]);
					nameLabel.SetText(Record[1]);
					timeLabel.SetText(Record[2]);
					tryLabel.SetText(Record[3]);
					
					if(Record[4] != "-1")
						rsLabel.SetText(Record[4]);
					else
						rsLabel.SetText("?");
					
					frame.Visible = True;
				} else {
					frame.Visible = False;
				}				
			}
		}
	}
--></script>
</manialink>
	""";
}

Void Refresh(Text[][] _Records) {
	G_Records = _Records;
	foreach(Player in AllPlayers) {
		UpdateManialink(Player);
	}
}
