#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Editors/Index.Script.txt"			as Editors
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Credits/Index.Script.txt"			as Credits
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Play/Index.Script.txt"			as Play
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Home/Index.Script.txt"			as Home
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Windows.Script.txt"				as Windows
#Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/Index.Script.txt"				as Dialogs
#Include "Libs/miltant/Nadeo_Fork/Common/Core/MenuLayers.Script.txt"				as Layers
#Include "Libs/miltant/ObstacleV2/ManialinkLib.Script.txt"							as Styles
#Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/JoinServer/Index.Script.txt"	as JoinDialog
// #Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/CreateServer/Index.Script.txt"	as CreateDialog

declare Text G_ReqServerLogin;
declare Boolean G_ReqCreateServer;

Text Build() {
	return """
<manialink version="3" name="L_Menus">
	{{{Styles::GetStyleSheet()}}}
	<framemodel id="Model_MenuItem">
		<quad halign="center" valign="center2" size="33.33 15" id="Quad_Trigger" scriptevents="1" data-target="Label_Text" data-anim-cursor="Quad_Cursor"/>

		<label halign="center" valign="center2" textprefix="$t" textsize="5.5" class="lato" pos="0 -1" id="Label_Text" textcolor="fff"/>
	</framemodel>

	<frame pos="-160 90">
		<quad pos="60 -8" size="30 15" halign="center" valign="center2" id="Quad_Home" zindex="1"
			data-name="home" data-target="Label_Home" scriptevents="1" data-anim-cursor="Quad_Cursor"
			data-default-anim="<label scale=&quot;1.&quot;/>" data-target-anim="<label scale=&quot;1.1&quot;/>"/>
		<label text="ï€•" class="lato" pos="69.58 -9" halign="center" valign="center2" textsize="5" id="Label_Home"/>

		<frame id="Frame_Menus">
			<frameinstance pos="94 -7.66" data-name="editors" modelid="Model_MenuItem"/>
			<frameinstance pos="127.33 -7.66" data-name="profile" modelid="Model_MenuItem" data-preventdefault="True"/>
			<frameinstance pos="160.67 -7.66" data-name="play" modelid="Model_MenuItem"/>
			<frameinstance pos="194 -7.66" data-name="credits" modelid="Model_MenuItem"/>
			<frameinstance pos="227.33 -7.66" data-name="quit" modelid="Model_MenuItem" data-preventdefault="True"/>
		</frame>

		<quad pos="160.67 2.33" size="0 5.67" style="Bgs1" substyle="BgTitle3_3" halign="center" id="Quad_Cursor"/>
		<quad pos="46.5 -3.67" size="12.5 10.17" image="file://Media/Images/Obstacle/Menu/Logo.png"/>
		<quad pos="30" size="260 18.83" image="file://Media/Images/Obstacle/Menu/FHeader.png" keepratio="Fit"/>
	</frame>

	<script><![CDATA[
		#Include "MathLib" as ML
		#Include "TextLib" as TL
		#Include "ColorLib" as CL

		{{{Styles::GetDeclareGlobals()}}}

		main() {
 			{{{Styles::GetInitFrameMenus()}}}

			EnableMenuNavigationInputs = True;

			foreach (MenuItem in Frame_Menus.Controls) {
				declare Frame_MenuItem = (MenuItem as CMlFrame);
				declare Label_Text = (Frame_MenuItem.GetFirstChild("Label_Text") as CMlLabel);

				if (Frame_MenuItem.DataAttributeExists("name")) {
					Label_Text.SetText(Frame_MenuItem.DataAttributeGet("name"));
				}
			}

			declare Quad_Home = Page.GetFirstChild("Quad_Home");
			ManialinkLib_OnClickMenu(Quad_Home);

			declare LastEscapePress = False;
			while(True) {
				yield;

				if (!PageIsVisible && PendingEvents.count == 0) continue;

				{{{Styles::GetLoopActiveItem()}}}

				if (IsKeyPressed(36)) {
					if (!LastEscapePress) {
						SendCustomEvent("escape_pressed", []);
						ManialinkLib_OnClickMenu(Quad_Home);
					}

					LastEscapePress = True;
				} else {
					LastEscapePress = False;
				}
			}
		}
	]]></script>
</manialink>""";
}

Void Load() {
	declare LayerName = "L_Menus";
	declare Text MLText = Build();

	Layers::Create(LayerName, MLText);
	Layers::Attach(LayerName);

	Editors::Load();
	Play::Load();
	Credits::Load();

	Home::Load();
	Windows::Load(Home::GetName());

    JoinDialog::Load();
	G_ReqCreateServer = False;
}

Void JoinServer() {
	if (G_ReqServerLogin != "" && JoinDialog::Join(G_ReqServerLogin)) {
		G_ReqServerLogin = "";
	}
}

Void CreateServer() {
	// if (G_ReqCreateServer && CreateDialog::Create()) {
	// 	G_ReqCreateServer = False;
	// }
}



Void Loop() {
	foreach(Event in PendingEvents) {
		if (Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
			if (Event.CustomEventData.count > 0) {
				switch (Event.CustomEventType) {
					case "menu_item_click": {
						switch (Event.CustomEventData[0]) {
							case "home": {
								Windows::Load(Home::GetName());
							}
			
							case "editors": {
								Windows::Load(Editors::GetName());
							}
			
							case "profile": {
								Menu_Profile();
								yield;
							}
			
							case "play": {
								Windows::Load(Play::GetName());
							}
			
							case "credits": {
								Windows::Load(Credits::GetName());
							}
			
							case "quit": {
								Menu_Quit();
							}
						}
					}
					case "join_server": {
						G_ReqServerLogin = Event.CustomEventData[0];
					}
					case "create_server": {
						G_ReqCreateServer = True;
					}
				}
			} else if (Event.CustomEventType == "escape_pressed") {
				declare CurrentWindow = Layers::Get(Windows::Current());

				if (Dialogs::Current() == "") {
					if (Windows::Current() == "L_Window_home" && !(CurrentWindow == Null || CurrentWindow.AnimInProgress)) {
						Menu_Quit();
					} else {
						Windows::Load("home");
					}
				}
			}
		}
	}

	Home::Loop();
	Editors::Loop();
	Credits::Loop();
	Play::Loop();
	Windows::Loop();
	JoinServer();
	CreateServer();
}