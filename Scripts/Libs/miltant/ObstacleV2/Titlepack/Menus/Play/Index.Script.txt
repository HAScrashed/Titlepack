#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Windows.Script.txt"				as Windows
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/SubMenu/Index.Script.txt"			as SubMenu
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Play/Campaign/Index.Script.txt"	as Campaign
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Play/Servers/Index.Script.txt"	as Servers
#Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/Explorer/Index.Script.txt"		as ExplorerDialog

declare Boolean G_PendingStartLocalPlay;

Text GetName() {
	return "play";
}

Void StartLocalPlay() {
	if (G_PendingStartLocalPlay)
		G_PendingStartLocalPlay = !ExplorerDialog::StartLocalPlay();
}

Void Load() {
	declare SubMenu::K_SubMenu[] SubMenus;

	declare SubMenu::K_SubMenu Campaign;
	Campaign.Name = "campaign";
	Campaign.Image = "file://Media/Images/Obstacle/Moods/campaign.png";
	SubMenus.add(Campaign);
	Campaign::Load();

	declare SubMenu::K_SubMenu Online;
	Online.Name = "online";
	Online.Image = "file://Media/Images/Obstacle/Moods/online.png";
	SubMenus.add(Online);
	Servers::Load();

	declare SubMenu::K_SubMenu Local;
	Local.Name = "local";
	Local.Image = "file://Media/Images/Obstacle/Moods/local.png";
	Local.PreventDefault = True;
	SubMenus.add(Local);

	declare SubMenu::K_SubMenu Tutorials;
	Tutorials.Name = "tutorials";
	Tutorials.Image = "file://Media/Images/Obstacle/Moods/tutorials.png";
	Tutorials.PreventDefault = True;
	SubMenus.add(Tutorials);


	declare Windows::K_Window Window;
	Window.Name = GetName();
	Window.Manialink = SubMenu::Build(SubMenus);
	Window.Animation = CUILayer::EUILayerAnimation::LeftSlow;

	Windows::CreateWindow(Window);
}

Void Loop() {
	Campaign::Loop();
	Servers::Loop();

	foreach(Event in PendingEvents) {
		if ( Event.Type == CManiaAppEvent::EType::LayerCustomEvent &&
			 Event.CustomEventData.count > 0 &&
			 Event.CustomEventType == "window_load") {
			switch (Event.CustomEventData[0]) {
				case "local": {
					ExplorerDialog::Load("Map");
					G_PendingStartLocalPlay = True;
				}
				case "tutorials": {
					wait(TitleControl.IsReady);

					declare Language = "English";

					declare Languages = [
						// "cs" => "Czech",
						// "da" => "Danish",
						// "de" => "German",
						// en, en_GB
						// "es" => "Spanish",
						// "fi" => "Finnish",
						"fr" => "French",
						// "hu" => "Hungarian",
						// "it" => "Italian",
						// "ja" => "Japanese",
						// "ko" => "Korean",
						// "nb" => "Norwegian",
						// "nl" => "Dutch",
						// "pl" => "Polish",
						// "pt" => "Portuguese",
						// "pt_BR" => "Portuguese",
						// "ro" => "Romanian",
						"ru" => "Russian"//,
						// "sk" => "Slovak",
						// "sv" => "Swedish",
						// "tr" => "Turkish",
						// "zh_CN" => "Chinese"
					];
					
					if (Languages.exists(LocalUser.Language))
						Language = Languages[LocalUser.Language];
					
					declare Path = "MEDIABROWSER_HiddenResources\\tuto\\" ^ Language ^ ".Map.Gbx";

					TitleControl.PlayMap(Path, "Shootmania/Obstacle.Script.txt", "<mode_script_settings><setting name='S_EnableRecords' value='False' type='boolean'/></mode_script_settings>");
				}
			}
		}
	}

	StartLocalPlay();
}