Text Build() {
	return """
<manialink version="3" name="L_Background">
	<frame z-index="-1000">
		<label id="Label_Mute" halign="center" valign="center" size="20 20" focusareacolor1="0000" focusareacolor2="0000"
			text="" textcolor="ffff" pos="144 -80" textsize="7" opacity="0.5" scriptevents="1" z-index="5"/>
		<quad id="Quad_Background" halign="center" valign="center" size="320 180"/>
	</frame>
	
	
	<script><![CDATA[

		declare CAudioSource G_Music;
		declare CVideo G_Video;

		Void StopMusic()
		{
			if (G_Music != Null)
			{
				G_Music.Stop();
				Audio.DestroySound(G_Music);
				G_Music = Null;
			}
			Audio.LimitMusicVolumedB = -100.;
		}

		Void PlayMusic(Text _MusicPath)
		{
			if (G_Music != Null)
			{
				G_Music.Stop();
				Audio.DestroySound(G_Music);
				G_Music = Null;
			}
			if (_MusicPath == "") {
				return;
			}
			Audio.LimitMusicVolumedB = 0.;
			G_Music = Audio.CreateSound(_MusicPath, 1., True, True, False);
			G_Music.FadeDuration = 0.;
			G_Music.Stop();
			G_Music.Play();
		}

		Void StopVideo()
		{
			if (G_Video != Null)
			{
				G_Video.Stop();
			}
		}

		Void PlayVideo(Text _VideoPath, Boolean _Looping)
		{
			StopVideo();
			G_Video = Video.CreateVideo(_VideoPath, _Looping, True);
			G_Video.Play();
			// G_Video.Image.DefaultVideoTimer = CImage::EVideoTimer::Human;
		}

		Void PlayVideo(Text _VideoPath)
		{
			PlayVideo(_VideoPath, False);
		}

		main() {
			declare Quad_Background = (Page.GetFirstChild("Quad_Background") as CMlQuad);
			declare Label_Mute = (Page.GetFirstChild("Label_Mute") as CMlLabel);

			declare persistent ObsStore_ForceMusic for LocalUser = True;
			declare LastInteraction = Now;

			PlayMusic("file://Media/Musics/Menu/opening.ogg");
			PlayVideo("file://Media/Images/Logos/opening_intro.webm");
			Quad_Background.Image = G_Video.Image;

			Audio.ForceEnableMusic = ObsStore_ForceMusic;
			if (Audio.ForceEnableMusic) {
				Label_Mute.SetText("");
			} else {
				Label_Mute.SetText("");
			}

			while(True) {
				yield;

				if (LastInteraction + 10000 < Now) {
					Label_Mute.Opacity = .5;
				} else if (Label_Mute.Opacity < 1.) {
					Label_Mute.Opacity = 1.;
				}

				if (!G_Video.IsPlaying) {
					PlayVideo("file://Media/Images/Logos/opening_loop.webm", True);
					Quad_Background.Image = G_Video.Image;
				}
				if (G_Video.PlayCursor > 0. && G_Music.PlayCursor == 0.) {
					if (!Audio.ForceEnableMusic) {
						Audio.ForceEnableMusic = ObsStore_ForceMusic;
						G_Music.PlayCursor = G_Video.PlayCursor;

						LastInteraction = Now;
					}
				} else if (!ObsStore_ForceMusic && Audio.LimitMusicVolumedB == 0.) {
					Audio.LimitMusicVolumedB = -100.;
					LastInteraction = Now;
				}

				if (!PageIsVisible && PendingEvents.count == 0) continue;

				foreach(Event in PendingEvents) {
					switch(Event.Type) {
						case CMlEvent::Type::PluginCustomEvent: {
							switch(Event.CustomEventType) {
								case "Titlepack_Background_Hide" : {
									Quad_Background.Hide();
								}
								case "Titlepack_Background_Show" : {
									Quad_Background.Show();
								}
							}
						}
						case CMlEvent::Type::MouseClick: {
							if(Event.ControlId == "Label_Mute") {
								Audio.ForceEnableMusic = !Audio.ForceEnableMusic;
								ObsStore_ForceMusic = Audio.ForceEnableMusic;
								LastInteraction = Now;
								
								if (Audio.ForceEnableMusic) {
									Label_Mute.SetText("");
									Audio.LimitMusicVolumedB = 0.;
								} else {
									Label_Mute.SetText("");
									Audio.LimitMusicVolumedB = -100.;
								}
							}
						}
						case CMlEvent::Type::MouseOver: {
							if(Event.ControlId == "Label_Mute") {
								LastInteraction = Now;
							}
						}
					}
				}
			}
		}
	]]></script>
</manialink>
	""";
}