#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Editors/Index.Script.txt"			as Editors
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Credits/Index.Script.txt"			as Credits
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Play/Index.Script.txt"			as Play
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Home/Index.Script.txt"			as Home
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Windows.Script.txt"				as Windows
#Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/Index.Script.txt"				as Dialogs
#Include "Libs/miltant/Nadeo_Fork/Common/Core/MenuLayers.Script.txt"				as Layers
#Include "Libs/miltant/ObstacleV2/ManialinkLib.Script.txt"							as Styles
#Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/JoinServer/Index.Script.txt"	as JoinDialog
// #Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/CreateServer/Index.Script.txt"	as CreateDialog

declare Text G_ReqServerLogin;
declare Boolean G_ReqCreateServer;

Text Build() {
	declare ML_Text =  """
<manialink version="3" name="L_Menus">
	{{{Styles::GetStyleSheet()}}}
	<framemodel id="Model_MenuItem">
		<quad halign="center" valign="center2" size="25 7.17" id="Quad_Trigger" scriptevents="1"
			data-target="Label_Text Quad_Square Quad_Underline" data-anim-length="70"
			data-default-anim="<label pos=&quot;0 .83&quot;/> <quad rot=&quot;45&quot; scale=&quot;1&quot;/> <quad scale=&quot;1&quot;/>"
			data-target-anim="<label pos=&quot;0 0.5&quot;/> <quad rot=&quot;0&quot; scale=&quot;1.2&quot;/> <quad scale=&quot;0.92&quot;/>"/>

		<quad halign="center" valign="center" size="2 2" pos="0 -3.16" rot="45" image="file://Media/Images/Obstacle/Menu/Rectangle.png" id="Quad_Square"/>
		<quad halign="center" size="17 0.5" pos="0 -3" image="file://Media/Images/Obstacle/Menu/BNav.png" id="Quad_Underline"/>

		<label halign="center" valign="center2" textprefix="$t" class="lato" pos="0 .83" id="Label_Text" textcolor="fff"/>
	</framemodel>
	
	<frame pos="-160 90">
		<label text="" class="lato" pos="69.58 -8.33" halign="center" valign="center2"
			textsize="5" id="Label_Home" data-name="home" scriptevents="1"
			data-target="Label_Home"
			data-default-anim="<label scale=&quot;1.&quot;/>" data-target-anim="<label scale=&quot;1.066&quot;/>"/>

		<frame id="Frame_Menus">
			<frameinstance pos="97.5 -7.33" data-name="editors" modelid="Model_MenuItem"/>
			<frameinstance pos="125.83 -7.33" data-name="profile" modelid="Model_MenuItem" data-preventdefault="True"/>
			<frameinstance pos="159.88 -7.5" data-name="play" modelid="Model_MenuItem"/>
			<frameinstance pos="194.17 -7.33" data-name="credits" modelid="Model_MenuItem"/>
			<frameinstance pos="222.5 -7.33" data-name="quit" modelid="Model_MenuItem" data-preventdefault="True"/>
		</frame>

		<label text="" class="lato" pos="241.67 -5.6" textsize="5" hidden="1"/>
		<label text="1000" pos="249.33 -6.33" textsize="3" class="lato" hidden="1"/>

		<quad pos="47.17 -3.67" size="11.17 8.83" image="file://Media/Images/Obstacle/Menu/Logo.png"/>
		<quad pos="30" size="260 18.83" image="file://Media/Images/Obstacle/Menu/FHeader.png" scriptevents="1" keepratio="Fit"/>
	</frame>

	<script><![CDATA[
		#Include "MathLib" as ML
		#Include "TextLib" as TL
		#Include "ColorLib" as CL

		{{{Styles::GetDeclareGlobals()}}}
		
		main() {
 			{{{Styles::GetInitFrameMenus()}}}

			EnableMenuNavigationInputs = True;

			foreach (MenuItem in Frame_Menus.Controls) {
				declare Frame_MenuItem = (MenuItem as CMlFrame);
				declare Label_Text = (Frame_MenuItem.GetFirstChild("Label_Text") as CMlLabel);

				if (Frame_MenuItem.DataAttributeExists("name")) {
					Label_Text.SetText(Frame_MenuItem.DataAttributeGet("name"));

					if (Frame_MenuItem.DataAttributeGet("name") == "play") {
						declare Quad_Square = (Frame_MenuItem.GetFirstChild("Quad_Square") as CMlQuad);
						Label_Text.TextSizeReal = 3.6;
						Quad_Square.ChangeImageUrl("file://Media/Images/Obstacle/Menu/Rectangle_Play.png");
					}
				}
			}

			declare LastEscapePress = False;
			while(True) {
				yield;

				if (!PageIsVisible && PendingEvents.count == 0) continue;

				foreach(Event in PendingEvents) {
					{{{Styles::GetEventLoopActiveItem()}}}
				}

				if (IsKeyPressed(36)) {
					if (!LastEscapePress)
						SendCustomEvent("escape_pressed", []);

					LastEscapePress = True;
				} else {
					LastEscapePress = False;
				}
			}
		}
	]]></script>
</manialink>
	""";

	return ML_Text;
}

Void Load() {
	declare LayerName = "L_Menu";
	declare Text MLText = Build();

	Layers::Create(LayerName, MLText);
	Layers::Attach(LayerName);

	Editors::Load();
	Play::Load();
	Credits::Load();

	Home::Load();
	Windows::Load(Home::GetName());

    JoinDialog::Load();
	G_ReqCreateServer = False;
}

Void JoinServer() {
	if (G_ReqServerLogin != "" && JoinDialog::Join(G_ReqServerLogin)) {
		G_ReqServerLogin = "";
	}
}

Void CreateServer() {
	// if (G_ReqCreateServer && CreateDialog::Create()) {
	// 	G_ReqCreateServer = False;
	// }
}



Void Loop() {
	foreach(Event in PendingEvents) {
		if (Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
			if (Event.CustomEventData.count > 0) {
				switch (Event.CustomEventType) {
					case "menu_item_click": {
						switch (Event.CustomEventData[0]) {
							case "home": {
								Windows::Load(Home::GetName());
							}
			
							case "editors": {
								Windows::Load(Editors::GetName());
							}
			
							case "profile": {
								Menu_Profile();
								yield;
							}
			
							case "play": {
								Windows::Load(Play::GetName());
							}
			
							case "credits": {
								Windows::Load(Credits::GetName());
							}
			
							case "quit": {
								Menu_Quit();
							}
						}
					}
					case "join_server": {
						G_ReqServerLogin = Event.CustomEventData[0];
					}
					case "create_server": {
						G_ReqCreateServer = True;
					}
				}
			} else if (Event.CustomEventType == "escape_pressed") {
				declare CurrentWindow = Layers::Get(Windows::Current());

				if (Dialogs::Current() == "") {
					if (Windows::Current() == "L_Window_home" && !(CurrentWindow == Null || CurrentWindow.AnimInProgress)) {
						Menu_Quit();
					} else {
						Windows::Load("home");
					}
				}
			}
		}
	}

	Home::Loop();
	Editors::Loop();
	Credits::Loop();
	Play::Loop();
	Windows::Loop();
	JoinServer();
	CreateServer();
}