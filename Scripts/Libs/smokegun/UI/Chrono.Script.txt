#Include "Libs/smokegun/Tools.Script.txt" as Tools

#Const  Version     "2018-10-26"
#Const  ScriptName  "Chrono.Script.txt"


Void UpdateLayer(CSmPlayer _Player) { }

Text GetLayer() {
    return """
<manialink version="3" name="Obstacle:Chrono">
    <frame id="Frame_Chrono" pos="0 -70">
        <label id="Label_Chrono" pos="0 -0.5" size="40 10" halign="center" valign="center2" text="00:00:00" textfont="OswaldMono" textsize="7"/>
        <quad pos="0 0" size="40 10" style="BgRaceScore2" substyle="BgCardPlayer" halign="center" valign="center"/>
    </frame>
<script><!--
    #Include "TextLib" as TL

    Integer GetTime(CPlayer _Player) {
        if (_Player == Null)
            return 0;
        declare netread Net_StartTime for _Player = 0;
        return GameTime - Net_StartTime;
    }

    main() {
        wait(InputPlayer != Null && Page != Null);

        declare Frame_Chrono        <=> (Page.GetFirstChild("Frame_Chrono") as CMlFrame);
        declare Label_Chrono        <=> (Frame_Chrono.GetFirstChild("Label_Chrono") as CMlLabel);
        Frame_Chrono.Visible = True;

        declare PrevUsedJump = False;
        declare PrevWarmup = False;
        while (True) {
            yield;

            if (!PageIsVisible)
                continue;

            declare LocalPlayer <=> InputPlayer;
            if (GUIPlayer != Null) LocalPlayer <=> GUIPlayer;

            declare netread Net_UsedJump for LocalPlayer = False;
            if (PrevUsedJump != Net_UsedJump) {
                PrevUsedJump = Net_UsedJump;

                // Set the color to red if the player has jumped
                if (Net_UsedJump)
                    Label_Chrono.TextColor = <0.7, 0., 0.>;
                else
                    Label_Chrono.TextColor = <1., 1., 1.>;
            }

            declare netread Net_IsWarmup for Teams[0] = False;
            if (PrevWarmup != Net_IsWarmup) {
                PrevWarmup = Net_IsWarmup;

                // Set the color to red if the player has jumped
                if (Net_IsWarmup)
                    Label_Chrono.TextColor = <1., 0.7, 0.>;
                else
                    Label_Chrono.TextColor = <1., 1., 1.>;
            }

            declare netread Net_StartTime for LocalPlayer = -1;
            declare LastStartTime for LocalPlayer = -1;

            if (Net_StartTime > 0) {
                if (!Frame_Chrono.Visible)
                    Frame_Chrono.Show();

                    Label_Chrono.Value = TL::TimeToText(GetTime(LocalPlayer), True);
            } else if (Frame_Chrono.Visible) {
                Frame_Chrono.Hide();
            }
        }
    }
--></script>
</manialink>""";
}
