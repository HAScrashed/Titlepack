#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Windows.Script.txt"			as Windows
#Include "Libs/miltant/ObstacleV2/ManialinkLib.Script.txt"						as Styles
#Include "MathLib"																as ML
#Include "Libs/miltant/ObstacleV2/ScrollBar.Script.txt"	 		       			as Scroll

#Const C_ColumnWidth 101.33
#Const C_ColumnCount 3
#Const C_PosX -160.
#Const C_PosY 65.
#Const C_Width 320.
#Const C_Height 155.

#Struct K_Role {
	Text[] Names;
	Text Date;
}

#Struct K_Person {
	Text Name;
	K_Role[] Roles;
}

#Struct K_Group {
	Text Title;
	K_Person[] People;
}

Text GetName() {
	return "credits";
}

/**
 * Returns the list of credits.
 * Order the groups in the order you want them to be displayed (left to right).
 * You might add empty groups to skip columns.
 * Order people by date of contribution (oldest first) then by name (alphabetical order).
 */
K_Group[] GetCredits() {
	return [
		K_Group {
			Title = "Brand Team",
			People = [
				K_Person { Name = "ZenyoX", Roles = [ K_Role { Names = ["Creative director"], Date = "2023 - Present" } ] },
				K_Person { Name = "Coco", Roles = [ K_Role { Names = ["Cinematic Artist"], Date = "2023" } ] },
				K_Person { Name = "HawKen", Roles = [ K_Role { Names = ["Cinematic Artist"], Date = "2024" } ] }
			]
		},
		K_Group {
			Title = "Mappers",
			People = [
				K_Person { Name = "Aurel", Roles = [ K_Role { Date = "Summer 2023, Winter 2024" } ] },
				K_Person { Name = "HasCrashed", Roles = [ K_Role { Date = "Summer 2023, Winter 2024" } ] },
				K_Person { Name = "Khass", Roles = [ K_Role { Date = "Summer 2023, Winter 2024" } ] },
				K_Person { Name = "Pixou", Roles = [ K_Role { Date = "Summer 2023, Winter 2024" } ] },
				K_Person { Name = "Batman", Roles = [ K_Role { Date = "Summer 2023" } ] },
				K_Person { Name = "Buggz", Roles = [ K_Role { Date = "Summer 2023" } ] },
				K_Person { Name = "Errorcat", Roles = [ K_Role { Date = "Summer 2023" } ] },
				K_Person { Name = "InnerPeace", Roles = [ K_Role { Date = "Summer 2023" } ] },
				K_Person { Name = "James", Roles = [ K_Role { Date = "Summer 2023" } ] },
				K_Person { Name = "Kiwi", Roles = [ K_Role { Date = "Summer 2023" } ] },
				K_Person { Name = "Blourk", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "Erkagon", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "Hawken", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "Lagaffe", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "Lyyras", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "Ouhou", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "Random", Roles = [ K_Role { Date = "Winter 2024" } ] },
				K_Person { Name = "SBVille", Roles = [ K_Role { Date = "Winter 2024" } ] }
			]
		},
		K_Group {
			Title = "Campaign Team",
			People = [
				K_Person { Name = "Khass", Roles = [ K_Role { Names = ["Campaign Referent"], Date = "Summer 2023, Winter 2024" } ] },
				K_Person { Name = "Hippe", Roles = [ K_Role { Names = ["Reference Times"], Date = "Summer 2023" } ] },
				K_Person { Name = "Kiwi", Roles = [ K_Role { Names = ["Reference Times"], Date = "Summer 2023" } ] },
				K_Person { Name = "UnMecRandom", Roles = [ K_Role { Names = ["Map testing"], Date = "Summer 2023" } ] },
				K_Person { Name = "Aurel", Roles = [ K_Role { Names = ["Campaign Referent", "Reference Times"], Date = "Winter 2024" } ] },
				K_Person { Name = "Booby", Roles = [ K_Role { Names = ["Campaign Referent"], Date = "Winter 2024" } ] },
				K_Person { Name = "Buggz", Roles = [ K_Role { Names = ["Campaign Referent"], Date = "Winter 2024" } ] },
				K_Person { Name = "Elise", Roles = [ K_Role { Names = ["Champion Times"], Date = "Winter 2024" } ] },
				K_Person { Name = "Kiwi", Roles = [ K_Role { Names = ["Champion Times"], Date = "Winter 2024" } ] },
				K_Person { Name = "Pixou", Roles = [ K_Role { Names = ["Champion Times"], Date = "Winter 2024" } ] },
				K_Person { Name = "UnMecRandom", Roles = [ K_Role { Names = ["Campaign Referent", "Reference Times"], Date = "Winter 2024" } ] },
				K_Person { Name = "SBVille", Roles = [ K_Role { Names = ["Champion Times"], Date = "Winter 2024" } ] }
			]
		},
		K_Group {
			Title = "Development Team",
			People = [
				K_Person { Name = "Nadeo", Roles = [ K_Role { Names = ["Game Developer"] } ] },
				K_Person { Name = "Steeffeen", Roles = [ K_Role { Names = ["Obstacle Developer"], Date = "2013 - 2017" } ] },
				K_Person { Name = "Gotagang", Roles = [ K_Role { Names = ["Obstacle Developer"], Date = "2017" } ] },
				K_Person { Name = "Smoke", Roles = [ K_Role { Names = ["Obstacle Developer"], Date = "2017 - 2021" } ] },
				K_Person { Name = "MiLTanT", Roles = [ K_Role { Names = ["Obstacle Developer"], Date = "2021 - Present" } ] },
				K_Person { Name = "Ahmad", Roles = [ K_Role { Names = ["Obstacle Developer"], Date = "2023 - Present" } ] },
				K_Person { Name = "HasCrashed", Roles = [ K_Role { Names = ["Dynamic Items"], Date = "2023 - Present" } ] }
			]
		},
		K_Group {},
		K_Group {},
		K_Group {
			Title = "Assets Creators",
			People = [
				K_Person { Name = "Titishu", Roles = [ K_Role { Names = ["BlocksKit", "MetalKit"], Date = "2013" } ] },
				K_Person { Name = "Aurel", Roles = [ K_Role { Names = ["Items Creator"], Date = "2013 - Present" } ] },
				K_Person { Name = "Elvestad", Roles = [ K_Role { Names = ["Titlepack image Artist"], Date = "2017" } ] },
				K_Person { Name = "HasCrashed", Roles = [ K_Role { Names = ["Textured Items", "NeonObjPack"], Date = "2017" } ] },
				K_Person { Name = "InnerPeace", Roles = [ K_Role { Names = ["Items Creator"], Date = "2019 - 2023" } ] },
				K_Person { Name = "HasCrashed", Roles = [ K_Role { Names = ["Invisible Skin"], Date = "2022" } ] },
				K_Person { Name = "Aurel InnerPeace", Roles = [ K_Role { Names = ["ColorPalette", "Missing Materials"], Date = "2023" } ] }
			]
		},
		K_Group {
			Title = "Tutorials",
			People = [
				K_Person { Name = "Aurel", Roles = [ K_Role { Names = ["Mapper", "Lead of translations", "English, French texts"], Date = "2022" } ] },
				K_Person { Name = "Bert", Roles = [ K_Role { Names = ["Russian texts"], Date = "2023" } ] },
				K_Person { Name = "Monk", Roles = [ K_Role { Names = ["Croatian texts"], Date = "2023" } ] },
				K_Person { Name = "Faker", Roles = [ K_Role { Names = ["Ukrainian texts"], Date = "2023" } ] }
			]
		}
	];
}

Text Build() {
	declare Text ML_Text = Styles::GetStyleSheet();
	declare TextOffsets = [C_ColumnWidth / 4. + 1.33, C_ColumnWidth / 4. * 3. + 2.];
	declare TextWidth = C_ColumnWidth / 2. - 1.667;
	declare Real[] ColumnOffsets;
	declare Real[] ColumnSizes;
	declare Groups = GetCredits();

	for (I, 0, C_ColumnCount - 1) {
		ColumnOffsets.add(I * C_ColumnWidth + I * 2. + 1.);
		ColumnSizes.add(0.);
	}

	foreach (IG => Group in Groups) {
		if (Group.People.count == 0)
			continue;

		declare ML_Group = """<label pos="55 -6" class="lato" size="96 10" text="{{{Group.Title}}}" halign="center" valign="center" textsize="4"/>""";
		declare CurAppendHeight = 12.;
		declare Col1Height = 0.;

		foreach (IP => Person in Group.People) {
			declare PosX = TextOffsets[IP % 2];
			declare ML_Person = """<label pos="{{{PosX}}} {{{-CurAppendHeight-6}}}" class="lato" size="{{{TextWidth}}} 5" text="{{{Person.Name}}}" halign="center" valign="center" textsize="4" textcolor="5D7FCE"/>""";
			
			declare PersonHeight = 7.;
			foreach (IR => Role in Person.Roles) {
				declare Text ML_Role = "";
				foreach (IR => Name in Role.Names) {
					PersonHeight += 5.;
					ML_Role ^= """
						<label pos="{{{PosX}}} {{{-CurAppendHeight-PersonHeight}}}" class="lato" size="{{{TextWidth}}} 4" text="{{{Name}}}" halign="center" valign="center" textsize="3"/>
					""";
				}
				if (Role.Date != "") {
					PersonHeight += 5.;
					ML_Role ^= """
						<label pos="{{{PosX}}} {{{-CurAppendHeight-PersonHeight}}}" class="lato" size="{{{TextWidth}}} 3" text="{{{Role.Date}}}" halign="center" valign="center" textsize="2" textcolor="5D7FCE"/>
					""";
				}
				ML_Person ^= ML_Role;
			}

			if (IP % 2 == 0) {
				Col1Height = PersonHeight;
			} else {
				CurAppendHeight += ML::Max(Col1Height, PersonHeight) + 3.;
				Col1Height = 0.;
			}

			ML_Group ^= ML_Person;
		}

		if (Col1Height > 0.) {
			CurAppendHeight += Col1Height + 3.;
		}

		declare GroupColumn = IG % C_ColumnCount;
		declare GroupHeight = CurAppendHeight + 5.;
		ML_Text ^= """
			<frame pos="{{{ColumnOffsets[GroupColumn]}}} {{{-ColumnSizes[GroupColumn]}}}" size="103.33 {{{GroupHeight}}}">
				{{{ML_Group}}}
				<quad pos="1" size="101.33 13" image="file://Media/Images/Obstacle/Credits/BHeader.png" keepratio="Fit"/>
				<quad size="103.33 {{{GroupHeight - 9.}}}" keepratio="Fit" pos="0 -9" style="Bgs1InRace" substyle="BgProgressBar" opacity="0.5"/>
			</frame>
		""";
		ColumnSizes[GroupColumn] = ColumnSizes[GroupColumn] + GroupHeight + 1.;
	}

	declare MaxColumnHeight = 0.;
	foreach (Col in ColumnSizes)
		MaxColumnHeight = ML::Max(MaxColumnHeight, Col);

	declare MinColumnHeight = MaxColumnHeight;
	declare MinColumnIndex = 0;
	foreach (I => Col in ColumnSizes)
		if (Col < MinColumnHeight) {
			MinColumnHeight = Col;
			MinColumnIndex = I;
		}

	declare DisclaimerHeight = 33.;
	ML_Text ^= """
		<label text="This page aims to list members of the community whose content is embeded in the obstacle titlepack $w-$z feel like we forgot you? Let us know on Discord!"
			pos="{{{ColumnOffsets[MinColumnIndex] + 103.33/2.}}} {{{-MinColumnHeight - DisclaimerHeight/2. + 1}}}" size="101.33 {{{DisclaimerHeight - 2}}}"
			autonewline="1" class="lato" halign="center" valign="center" textsize="4"/>
		<quad size="103.33 {{{DisclaimerHeight}}}" pos="{{{ColumnOffsets[MinColumnIndex]}}} {{{-MinColumnHeight + 1}}}" style="Bgs1InRace" substyle="BgProgressBar" opacity="0.5"/>
	""";

	if (MinColumnHeight + DisclaimerHeight > MaxColumnHeight)
		MaxColumnHeight = MinColumnHeight + DisclaimerHeight;

	declare ScrollMax = ML::Max(0., MaxColumnHeight - C_Height + Groups.count / 3.);
	return """
		<frame pos="{{{C_PosX}}} {{{C_PosY}}}" scroll="1" scrollmax="0 {{{ScrollMax}}}" size="{{{C_Width}}} {{{C_Height}}}" id="Frame_Credits">
			{{{ML_Text}}}
			<quad scriptevents="1" size="{{{C_Width + 20.}}} {{{MaxColumnHeight + 99.}}}" pos="-10 10"/>
		</frame>
		{{{Scroll::GetScrollBarML(<C_PosX + C_Width - 8.5, C_PosY>, "Frame_Credits", "y")}}}
		<script><![CDATA[
			#Include "MathLib" as ML
			main() {
				{{{Scroll::GetInitFrames()}}}
				while (True) {
					{{{Scroll::GetLoop()}}}
					yield;
				}
			}
		]]></script>
	""";
}

Void Load() {
	declare Windows::K_Window Window;
	Window.Name = GetName();
	Window.Manialink = Build();
	Window.Animation = CUILayer::EUILayerAnimation::RightSlow;

	Windows::CreateWindow(Window);
}

Void Loop() {}